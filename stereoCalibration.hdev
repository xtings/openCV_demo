dev_update_off ()
* Set the image path
ImgPath := 'C:/Users/OptiPlex 7060/Desktop/2/'
* Read the first images to get their size
I := 1
read_image (ImageL, ImgPath + 'L/' + I$'02d')
read_image (ImageR, ImgPath + 'R/' + I$'02d')
* Reopen the windows with an appropriate size
dev_close_window ()
get_image_size (ImageL, WidthL, HeightL)
get_image_size (ImageR, WidthR, HeightR)
dev_open_window (0, 0, WidthL/2, HeightL/2, 'black', WindowHandle1)
set_display_font (WindowHandle1, 14, 'mono', 'true', 'false')
dev_open_window (0, WidthL + 12, WidthL/2, HeightL/2, 'black', WindowHandle2)
* Set the initial values for the internal camera parameters
gen_cam_par_area_scan_division (0.005, 0, 4.8e-006, 4.8e-006, WidthL / 2.0, HeightL / 2.0, WidthL, HeightL, StartCamParL)
StartCamParR := StartCamParL
* Create a calibration data model in which all calibration data
* including the image coordinates of the calibration marks and
* the observation poses of the calibration plate will be
* accumulated
create_calib_data ('calibration_object', 2, 1, CalibDataID)
set_calib_data_cam_param (CalibDataID, 0, [], StartCamParL)
set_calib_data_cam_param (CalibDataID, 1, [], StartCamParR)
set_calib_data_calib_object (CalibDataID, 0, '3_3.descr')
* Start the loop over the calibration images
for I := 1 to 13 by 1
    * Read and display the calibration images
    read_image (ImageL, ImgPath + 'L/' + I$'02d')
    read_image (ImageR, ImgPath + 'R/' + I$'02d')
    dev_set_window (WindowHandle1)
    dev_display (ImageL)
    dev_set_window (WindowHandle2)
    dev_display (ImageR)
    * Find the calibration plate and store observations
    * in the calibration data model
    find_calib_object (ImageL, CalibDataID, 0, 0, I, ['alpha'], 0.4)
    find_calib_object (ImageR, CalibDataID, 1, 0, I, ['alpha'], 0.4)
    * Visualize the extracted calibration marks and the
    * coordinate system defined by the estimated pose.
    visualize_observation_results (ImageL, CalibDataID, 0, I, WindowHandle1)
    visualize_observation_results (ImageR, CalibDataID, 1, I, WindowHandle2)
*     wait_seconds (.2)
endfor
* Perform the actual calibration
calibrate_cameras (CalibDataID, Errors)
* Get the calibrated camera parameters
get_calib_data (CalibDataID, 'camera', 0, 'params', CamParamL)
get_calib_data (CalibDataID, 'camera', 1, 'params', CamParamR)
* Since the left camera is the reference camera for the
* calib data model, the pose of the right camera is its
* pose relative to the left camera
get_calib_data (CalibDataID, 'camera', 1, 'pose', cLPcR)
* Store the results into files. Here, you can either store the
* individual results
write_cam_par (CamParamL, 'cam_left-125.dat')
write_cam_par (CamParamR, 'cam_right-125.dat')
write_pose (cLPcR, 'pos_right2left.dat')
* or you store the complete camera setup model and thus enable
* Generate the rectification maps
gen_binocular_rectification_map (MapL, MapR, CamParamL, CamParamR, cLPcR, 1, 'geometric', 'bilinear', RectCamParL, RectCamParR, CamPoseRectL, CamPoseRectR, RectLPosRectR)
* Read in a stereo image pair, aquired with the stereo camera system,
* which has been calibrated, just now.
read_image (ImageL, ImgPath + 'L/' + '31.jpg')
read_image (ImageR, ImgPath + 'R/' + '31.jpg')
* Rectify the stereo images and display them
map_image (ImageL, MapL, ImageRectifiedL)
map_image (ImageR, MapR, ImageRectifiedR)
dev_set_window (WindowHandle1)
dev_clear_window ()
dev_display (ImageRectifiedL)
disp_message (WindowHandle1, 'Rectified images', 'window', 12, 12, 'black', 'true')
dev_set_window (WindowHandle2)
dev_clear_window ()
dev_display (ImageRectifiedR)


intersect_lines_of_sight (RectCamParL, RectCamParR, RectLPosRectR, 326, 88, 326, 323, X1, Y1, Z1, Dist)
intersect_lines_of_sight (RectCamParL, RectCamParR, RectLPosRectR, 510, 955, 510, 1346, X2, Y2, Z2, Dist)
tuple_sqrt ((X1-X2)*(X1-X2)+(Y1-Y2)*(Y1-Y2)+(Z1-Z2)*(Z1-Z2), Sqrt)




clear_calib_data (CalibDataID)
